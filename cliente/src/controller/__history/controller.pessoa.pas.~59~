unit controller.pessoa;

interface

uses
  System.SysUtils,
  System.Classes,
  System.Generics.Collections,
  Data.Bind.Components,
  Data.Bind.ObjectScope,
  Data.Bind.GenData,
  Fmx.Bind.GenData,
  model.entity.endereco,
  model.entity.pessoa,
  model.enums,
  model.utils, System.Actions, FMX.ActnList, dao.pessoas;

type
  TControllerPessoa = class(TDataModule)
    pbsPessoa: TPrototypeBindSource;
    pbsEndereco: TPrototypeBindSource;
    actPessoas: TActionList;
    actSalvar: TAction;
    actNovo: TAction;
    actAlterar: TAction;
    actCancelar: TAction;
    actAtualizar: TAction;
    actExcluir: TAction;
    procedure pbsPessoaCreateAdapter(Sender: TObject;
      var ABindSourceAdapter: TBindSourceAdapter);
    procedure pbsEnderecoCreateAdapter(Sender: TObject;
      var ABindSourceAdapter: TBindSourceAdapter);
    procedure actSalvarExecute(Sender: TObject);
    procedure actNovoExecute(Sender: TObject);
    procedure actAlterarExecute(Sender: TObject);
    procedure actCancelarExecute(Sender: TObject);
    procedure actAtualizarExecute(Sender: TObject);
    procedure actExcluirExecute(Sender: TObject);
  private
    bsListaPessoas: TListBindSourceAdapter<TPessoa>;
    bsEndereco: TObjectBindSourceAdapter<TEndereco>;
    FListaPessoas: TObjectList<TPessoa>;
    FEndereco: TEndereco;

    procedure PessoasAfterScroll(Adapter: TBindSourceAdapter);
    procedure PessoasAfterPost(Adapter: TBindSourceAdapter);
  public
    procedure InicializarPessoas;
    procedure CarregarPessoas;
    procedure Novo;
    procedure Editar;
    procedure Salvar;
    procedure Cancelar;
    procedure Excluir;
    procedure AtualizarNatureza(aNatureza: Integer);
    procedure CadastrarLote(aJSON: String);
  end;

var
  ControllerPessoa: TControllerPessoa;

implementation

{%CLASSGROUP 'FMX.Controls.TControl'}

uses view.principal, FMX.TabControl, view.cadastro, System.JSON;
{$R *.dfm}

procedure TControllerPessoa.actAlterarExecute(Sender: TObject);
begin
  Editar;
end;

procedure TControllerPessoa.actAtualizarExecute(Sender: TObject);
begin
  CarregarPessoas;
end;

procedure TControllerPessoa.actCancelarExecute(Sender: TObject);
begin
  Cancelar;
end;

procedure TControllerPessoa.actExcluirExecute(Sender: TObject);
begin
  Excluir;
end;

procedure TControllerPessoa.actNovoExecute(Sender: TObject);
begin
  Novo;
end;

procedure TControllerPessoa.actSalvarExecute(Sender: TObject);
begin
  Salvar;
end;

procedure TControllerPessoa.AtualizarNatureza(aNatureza: Integer);
begin
  TPessoa(pbsPessoa.InternalAdapter.Current).FlNatureza := TNaturezaPessoa(aNatureza);
end;

procedure TControllerPessoa.CadastrarLote(aJSON: String);
begin
  daoPessoas.InserirJSONLote(aJSON);
  frmPrincipal.tbConteudo.SetActiveTabWithTransition
    (frmPrincipal.tbiLista,
    TTabTransition.Slide, TTabTransitionDirection.Reversed);
end;

procedure TControllerPessoa.Cancelar;
begin
  bsListaPessoas.Cancel;
  frmPrincipal.tbConteudo.SetActiveTabWithTransition
    (frmPrincipal.tbiLista,
    TTabTransition.Slide, TTabTransitionDirection.Reversed);
end;

procedure TControllerPessoa.CarregarPessoas;
begin
  daoPessoas.Carregar(FListaPessoas);
  bsListaPessoas.Refresh;
  bsListaPessoas.First;
end;

procedure TControllerPessoa.Editar;
begin
  bsListaPessoas.Edit;
  frmPrincipal.tbConteudo.SetActiveTabWithTransition
    (frmPrincipal.tbiCadastro,
    TTabTransition.Slide, TTabTransitionDirection.Normal);
end;

procedure TControllerPessoa.Excluir;
var
  vPessoa: TPessoa;
begin
  vPessoa := TPessoa(pbsPessoa.InternalAdapter.Current);
  daoPessoas.Excluir(vPessoa);
  CarregarPessoas;
end;

procedure TControllerPessoa.InicializarPessoas;
begin
  if not Assigned(FListaPessoas) then
  begin
    FListaPessoas := TObjectList<TPessoa>.Create;
  end;

  bsListaPessoas.SetList(FListaPessoas, True);
  bsListaPessoas.Active := True;

  CarregarPessoas;
end;

procedure TControllerPessoa.Novo;
begin
  bsListaPessoas.Insert;
  frmPrincipal.tbConteudo.SetActiveTabWithTransition
    (frmPrincipal.tbiCadastro,
    TTabTransition.Slide, TTabTransitionDirection.Normal);
end;

procedure TControllerPessoa.pbsEnderecoCreateAdapter(Sender: TObject;
  var ABindSourceAdapter: TBindSourceAdapter);
begin
  bsEndereco := TObjectBindSourceAdapter<TEndereco>.Create(nil, FEndereco, False);
  ABindSourceAdapter := bsEndereco;
end;

procedure TControllerPessoa.pbsPessoaCreateAdapter(Sender: TObject;
  var ABindSourceAdapter: TBindSourceAdapter);
begin
  bsListaPessoas := TListBindSourceAdapter<TPessoa>.Create(nil,FListaPessoas,True);

  ABindSourceAdapter := bsListaPessoas;
  bsListaPessoas.AfterScroll := PessoasAfterScroll;
  bsListaPessoas.AfterPost := PessoasAfterPost;

  InicializarPessoas;
end;

procedure TControllerPessoa.PessoasAfterPost(Adapter: TBindSourceAdapter);
var
  vPessoa: TPessoa;
begin
  vPessoa := TPessoa(pbsPessoa.InternalAdapter.Current);
  if vPessoa.IdPessoa = 0 then
  begin
    daoPessoas.Nova(vPessoa);
  end else
  begin
    daoPessoas.Atualizar(vPessoa);
  end;
end;

procedure TControllerPessoa.PessoasAfterScroll(Adapter: TBindSourceAdapter);
begin
  FEndereco := bsListaPessoas.List[bsListaPessoas.CurrentIndex].endereco;
  bsEndereco.SetDataObject(FEndereco, False);
  bsEndereco.Active := True;
end;

procedure TControllerPessoa.Salvar;
begin
  if bsListaPessoas.State in [seInsert, seEdit] then
  begin
    if bsEndereco.State in [seInsert, seEdit] then
    begin
      bsEndereco.Post;
    end;

    bsListaPessoas.Post;
  end;

  CarregarPessoas;

  frmPrincipal.tbConteudo.SetActiveTabWithTransition
    (frmPrincipal.tbiLista,
    TTabTransition.Slide, TTabTransitionDirection.Reversed);
end;

end.
